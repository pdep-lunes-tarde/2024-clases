import wollok.game.*

import example.*

object jueguito {
	method reset() {
		scoreboard.reset()
		self.spawnManzana()
	}

	method configurar() {
		self.configurarControles()
		self.agregarElementosIniciales()
	}

	method configurarControles() {
		keyboard.right().onPressDo {
			snake.cambiarDireccion(derecha)
		}
		keyboard.up().onPressDo {
			snake.cambiarDireccion(arriba)
		}
		keyboard.left().onPressDo {
			snake.cambiarDireccion(izquierda)
		}
		keyboard.down().onPressDo {
			snake.cambiarDireccion(abajo)
		}
	}

	method agregarElementosIniciales() {
		snake.empezaEn(game.center())
		game.addVisual(snake)
		game.onTick(
			300, "moverse", {
				snake.move()
			})

		game.addVisual(pared)
		game.onCollideDo(snake, { otro =>
			otro.chocasteConSnake(snake)
		})
		self.spawnManzana()
		game.addVisual(scoreboard)
	}

	method spawnManzana() {
		const manzana = new Manzana(posicion =
			game.at(1.randomUpTo(game.width() - 1),
					1.randomUpTo(game.height() - 1))
		)
		game.addVisual(manzana)
	}

	method seAgarroUnaManzana(manzana) {
		scoreboard.aumentarPuntaje(100)
    	game.removeVisual(manzana)
		self.spawnManzana()
	}
}

program viboritaGame {
	game.height(15)
	game.width(15)
	game.cellSize(32)

	jueguito.configurar()

	game.start()
}
